Barra de Progreso SVG = 
VAR ValueRaw = COALESCE ( [/*==== Medida de Progreso ======*/], 0 )
VAR Value2   = MIN ( 1, MAX ( 0, ValueRaw ) )
VAR PctTxt   = FORMAT ( Value2, "0.00%" )

-- Tamaño de barra
VAR TotalW = 200
VAR TotalH = 26

-- Padding: Espacio para bordes
VAR PadL = 10
VAR PadR = 10
VAR PadT = 5
VAR BarH = 16

VAR InnerW = TotalW - PadL - PadR
VAR FillW  = ROUND ( InnerW * Value2, 0 )

-- Colores
VAR BgColor   = "#E6E6E6"  -- Color de Fondo
VAR FillColor = "#06234d"  -- Color de barra

-- Texto
VAR FontSize = 11
VAR TextPadRight = 6   -- Espacio pegado al borde derecho de la barra

-- Umbral: ancho mínimo para que el texto quede dentro de la barra
VAR MinFillForInside = 46

VAR UseInside = FillW >= MinFillForInside

-- Condición para cambiar de color
VAR TextColor =
    IF ( UseInside, "#FFFFFF", "#06234d" )

-- Posiciones
VAR BgX = PadL
VAR BgY = PadT
VAR Rx  = 0  --redondeo de borde de barra

VAR TextX =
    IF(
        UseInside,
        BgX + FillW - TextPadRight,     -- casi pegado al final de la barra azul
        PadL + ( InnerW / 2 )           -- si no entra, al medio
    )

VAR TextAnchor =
    IF( UseInside, "end", "middle" )

VAR TextY = BgY + ( BarH / 2 )

RETURN
"data:image/svg+xml;utf8," &
"<svg xmlns='http://www.w3.org/2000/svg' width='" & TotalW & "' height='" & TotalH & "' viewBox='0 0 " & TotalW & " " & TotalH & "'>" &
    "<rect x='" & BgX & "' y='" & BgY & "' width='" & InnerW & "' height='" & BarH & "' rx='" & Rx & "' ry='" & Rx & "' fill='" & BgColor & "'/>" &
    "<rect x='" & BgX & "' y='" & BgY & "' width='" & FillW & "' height='" & BarH & "' rx='" & Rx & "' ry='" & Rx & "' fill='" & FillColor & "'/>" &
    "<text x='" & TextX & "' y='" & TextY & "'" &
        " text-anchor='" & TextAnchor & "'" &
        " dominant-baseline='middle'" &
        " font-family='Segoe UI'" &
        " font-size='" & FontSize & "'" &
        " font-weight='600'" &
        " fill='" & TextColor & "'">" &
        PctTxt &
    "</text>" &
"</svg>"
